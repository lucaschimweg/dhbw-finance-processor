plugins {
    id 'java'
    id 'jacoco'
}

sourceCompatibility = 1.15
targetCompatibility = 1.15

sourceSets {
    main

    defaultTransformationsPlugin {
        compileClasspath += sourceSets.main.output
        java {
            srcDir 'src/plugins/defaultTransformations/java'
        }

        resources {
            srcDir 'src/plugins/defaultTransformations/resources'
        }
    }


    jsonPlugin {
        compileClasspath += sourceSets.main.output

        java {
            srcDir 'src/plugins/json/java'
        }

        resources {
            srcDir 'src/plugins/json/resources'
        }
    }


    test {
        runtimeClasspath += sourceSets.defaultTransformationsPlugin.output
        runtimeClasspath += sourceSets.jsonPlugin.output
        compileClasspath += sourceSets.defaultTransformationsPlugin.output
        compileClasspath += sourceSets.jsonPlugin.output
    }
}

group 'net.schimweg.finproc'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    getAdditionalSourceDirs().setFrom(
            files(sourceSets.main.allSource.srcDirs),
            files(sourceSets.jsonPlugin.allSource.srcDirs),
            files(sourceSets.defaultTransformationsPlugin.allSource.srcDirs)
    )

    getAdditionalClassDirs().setFrom(
            files(sourceSets.main.output),
            files(sourceSets.jsonPlugin.output),
            files(sourceSets.defaultTransformationsPlugin.output)
    )

    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

dependencies {
    implementation group: 'org.yaml', name: 'snakeyaml', version: '1.28'
    jsonPluginImplementation 'com.google.code.gson:gson:2.8.6'
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")
}

task buildJar(type: Jar) {
    destinationDirectory = file("run/")

    from(sourceSets.main.output) {
        include '**/*'
    }

    manifest {
        attributes(
            'Main-Class': 'net.schimweg.financeProcessor.Main'
        )
    }
}

task exportDefaultTransformationsJar(type: Jar) {
    destinationDirectory = file("run/plugins/")
    archiveFileName.set("defaultTransformations.jar")

    from(sourceSets.defaultTransformationsPlugin.output) {
        include '**/*'
    }
}

task exportJsonJar(type: Jar) {
    destinationDirectory = file("run/plugins/")
    archiveFileName.set("json.jar")

    from(sourceSets.jsonPlugin.output) {
        include '**/*'
    }

    from {
        configurations.jsonPluginRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
